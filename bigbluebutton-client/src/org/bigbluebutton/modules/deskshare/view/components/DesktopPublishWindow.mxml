<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton open source conferencing system - http://www.bigbluebutton.org
  
  Copyright (c) 2010 BigBlueButton Inc. and by respective authors (see below).
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 2.1 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
-->

<dspub:MDIWindow 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	implements="org.bigbluebutton.common.IBbbModuleWindow"
	xmlns:mate="http://mate.asfusion.com/"
	xmlns:dspub="flexlib.mdi.containers.*"
	creationComplete="onCreationComplete()"
	verticalScrollPolicy="off" horizontalScrollPolicy="off"
	width="320" height="240"
	title="{ResourceUtil.getInstance().getString('bbb.desktopPublish.title')}"
	resizable="false">
		
	<mate:Listener type="{AppletStartedEvent.APPLET_STARTED}" method="onAppletStart" />
	<mate:Listener type="{MadePresenterEvent.SWITCH_TO_PRESENTER_MODE}" method="onChangedPresenter" />
	<mate:Listener type="{MadePresenterEvent.SWITCH_TO_VIEWER_MODE}" method="onChangedPresenter" />	
	<mate:Listener type="{CursorEvent.UPDATE_CURSOR_LOC_EVENT}" method="onUpdateCursorEvent" />
	<mate:Listener type="{ViewStreamEvent.STOP}" method="closePublishWindow" />
	
	<mx:Script>
		<![CDATA[
			import flexlib.scheduling.scheduleClasses.BackgroundItem;
			import com.asfusion.mate.events.Dispatcher;
			
			import flexlib.mdi.events.MDIWindowEvent;
			
			import mx.core.UIComponent;
			
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.LogUtil;
			import org.bigbluebutton.main.events.BBBEvent;
			import org.bigbluebutton.main.events.MadePresenterEvent;
			import org.bigbluebutton.main.views.MainCanvas;
			import org.bigbluebutton.modules.deskshare.events.AppletStartedEvent;
			import org.bigbluebutton.modules.deskshare.events.CursorEvent;
			import org.bigbluebutton.modules.deskshare.events.ShareWindowEvent;
			import org.bigbluebutton.modules.deskshare.events.StreamEvent;
			import org.bigbluebutton.modules.deskshare.events.ViewStreamEvent;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			
			public static const SCALE:Number = 5;
			private static const VID_HEIGHT_PAD:Number = 73;
			private static const VID_WIDTH_PAD:Number = 6;
			
			private var images:Images = new Images();
			[Bindable] public var bbbLogo:Class = images.bbb_logo;
			[Bindable] private var cursorIcon:Class = images.cursorIcon;
			
			private var connection:NetConnection;
			private var uri:String;
			private var room:String;
			private var sharingFullScreen:Boolean = false;
			private var streaming:Boolean = false;
			
			private var video:Video;
			private var ns:NetStream;
			private var videoHolder:UIComponent;
			private var stream:String;
			private var videoHeight:Number;
			private var videoWidth:Number;
			private var captureHeight:Number = Capabilities.screenResolutionY;
			private var captureWidth:Number = Capabilities.screenResolutionX;
			private var autoStart:Boolean = false;
			private var globalDispatcher:Dispatcher = new Dispatcher();
			
			[Bindable] private var cursor:Sprite;
			
			private function onCreationComplete():void {
				windowControls.maximizeRestoreBtn.enabled = false;
				windowControls.maximizeRestoreBtn.toolTip = ResourceUtil.getInstance().getString('bbb.desktopPublish.maximizeRestoreBtn.toolTip');
				windowControls.closeBtn.toolTip = ResourceUtil.getInstance().getString('bbb.desktopPublish.closeBtn.toolTip');
				windowControls.minimizeBtn.toolTip = ResourceUtil.getInstance().getString('bbb.desktopPublish.minimizeBtn.toolTip');
				
				cursor = new Sprite();
				cursor.graphics.lineStyle(6, 0xFF0000, 0.6);
				cursor.graphics.drawCircle(0,0,3);		
				
				if (autoStart) {
					shareScreen(true);	
				}		
				currentState = "dispFullRegionControlBar";
			}
			
			public function get defaultWidth():int{
				return this.width;
			}
			
			public function get defaultHeight():int{
				return this.height;
			}
			
			public function set defaultHeight(height:int):void{
				this.height = height;
			}
			
			public function set defaultWidth(width:int):void{
				this.width = width;
			}
			
			public function getPrefferedPosition():String{
				return MainCanvas.DESKTOP_SHARING_PUBLISH;
			}

			/*
			 * Implement resizeable interface.
			 */			
			public function resetWidthAndHeight():void{/* do nothing */}
			
			public function initWindow(connection:NetConnection, uri:String, room:String, autoStart:Boolean):void {
				this.connection = connection;
				this.uri = uri;
				this.room = room;				
				this.autoStart = autoStart;		
			}
			
			private function shareScreen(fullScreen:Boolean):void {
				startSharing(connection, uri, room, fullScreen);
			}
			
			private function startSharing(connection:NetConnection, uri:String, room:String, fullScreen:Boolean):void {
				var captureX:Number = 0;
				var captureY:Number = 0;
				
				sharingFullScreen = fullScreen;
				currentState = "dispQuitControlBar";
				ExternalInterface.call("startApplet", uri, room, fullScreen);
			}
			
			public function stopSharing():void{
				if (streaming) {
					stopStream();
					var streamEvent:StreamEvent = new StreamEvent(StreamEvent.STOP);
					dispatchEvent(streamEvent);					
				}
				sharingFullScreen = false;
				streaming = false;
									
				closeWindow();
			}
			
			private function onUpdateCursorEvent(event:CursorEvent):void {	
				// DO NOT compute the x and y coordinate and assign directly to the cursorImg
                // as it results in a flickering and jerky mouse pointer (ralam jun 10, 2010).
				cursor.x = ((event.x/captureWidth)) * videoHolder.width;
				cursor.y = ((event.y/captureHeight)) * videoHolder.height;					
				cursorImg.visible = true;

				// Do not display cursor if they are outside the capture area.
                if (cursor.x < videoHolder.x) cursor.x = videoHolder.x;
                if (cursor.y < videoHolder.y) cursor.y = videoHolder.y;
                if (cursor.x > videoHolder.x + videoHolder.width) cursor.x = videoHolder.x + videoHolder.width;
                if (cursor.y > videoHolder.y + videoHolder.height) cursor.y = videoHolder.y + videoHolder.height;
                cursorImg.x = cursor.x;
                cursorImg.y = cursor.y;
			}
			
			private function onAppletStart(event:AppletStartedEvent):void{
				LogUtil.debug("DeskShareWindow::onAppletStart");
				startPreviewStream(connection, room, event.videoWidth, event.videoHeight);
				var streamEvent:StreamEvent = new StreamEvent(StreamEvent.START);
				streamEvent.videoWidth = event.videoWidth;
				streamEvent.videoHeight = event.videoHeight;
				dispatchEvent(streamEvent);
			}
			
			private function startPreviewStream(nc:NetConnection, streamName:String, capWidth:Number, capHeight:Number):void{				
				streaming = true;
				
				// Store capture dimensions so we can position cursor properly.
				captureWidth = capWidth;
				captureHeight = capHeight;
				
				videoHolder = new UIComponent();
				var vidW:Number = captureWidth;
				var vidH:Number = captureHeight;
				
				// Don't scale if capture dimension is smaller than window.
				if ((captureWidth > this.width - VID_WIDTH_PAD) && (captureHeight > this.height - VID_HEIGHT_PAD)) {
					vidW = this.width - VID_WIDTH_PAD;	
					vidH = this.height - VID_HEIGHT_PAD;					
				} 

				LogUtil.debug("deskshare preview[" + captureWidth + "," + captureHeight + "][" + vidW + "," + vidH + "]");
				video = new Video(vidW, vidH);
				video.width = vidW;
				video.height = vidH;
				videoHolder.width = vidW;
				videoHolder.height = vidH;
				videoHolder.addChild(video);
				videoHolder.addChild(cursor);
				videoHolder.addChild(cursorImg);
				cursor.visible = false;
				this.addChild(videoHolder);
											
				ns = new NetStream(nc);
				ns.addEventListener(AsyncErrorEvent.ASYNC_ERROR, onAsyncError);
				ns.addEventListener( NetStatusEvent.NET_STATUS, onNetStatus );
				ns.client = this;
				ns.bufferTime = 0;
				ns.receiveVideo(true);
				ns.receiveAudio(false);
				video.attachNetStream(ns);
				ns.play(streamName);
				
				btnClosePublish.enabled = true;
			}
			
			private function stopStream():void{
				streaming = false;
				this.removeChild(videoHolder);
				captureHeight = Capabilities.screenResolutionY;
				captureWidth = Capabilities.screenResolutionX;
				ns.close();
			}
			
			private function onAsyncError(e:AsyncErrorEvent):void{
				LogUtil.debug("VIdeoWindow::asyncerror " + e.toString());
			}
			
			private function onNetStatus(e:NetStatusEvent):void{
				switch(e.info.code){
				case "NetStream.Publish.Start":
					LogUtil.debug("NetStream.Publish.Start for broadcast stream " + stream);
					break;
				case "NetStream.Play.UnpublishNotify":
					LogUtil.debug("NetStream.Play.UnpublishNotify for broadcast stream " + stream);
					stopSharing();
					break;
				case "NetStream.Play.Start":
					LogUtil.debug("Netstatus: " + e.info.code);
					globalDispatcher.dispatchEvent(new BBBEvent(BBBEvent.DESKSHARE_STARTED));
				}
			}
			
			private function onChangedPresenter(e:Event):void{
				stopSharing();
				closeWindow();
			}

			private function closeWindow():void {			
				ExternalInterface.call("stopApplet");	
				dispatchEvent(new ShareWindowEvent(ShareWindowEvent.CLOSE));				
			}		
				
			/*
			 * Override the close handler. We want the Event Map to send a message to
			 * the MDIManager to close this window;
			 */
			override public function close(event:MouseEvent = null):void {
				stopSharing();
				closeWindow();
			}		
			
			override protected function resourcesChanged():void{
				super.resourcesChanged();
				this.title = ResourceUtil.getInstance().getString('bbb.desktopPublish.title');
			}
			
			private function closePublishWindow(event:ViewStreamEvent):void{
				stopStream();
			}
			
		]]>
	</mx:Script>

	<dspub:states>
	    <!--http://stackoverflow.com/questions/369120/why-does-mxstates-have-trouble-being-resolved-to-a-component-implementation-->
        <mx:State name="dispFullRegionControlBar">   
            <mx:AddChild>
				<mx:ControlBar id="fullRegionBottomBar">
					<mx:Button id="btnFSPublish" toolTip="{ResourceUtil.getInstance().getString('bbb.desktopPublish.fullscreen.tooltip')}" label="{ResourceUtil.getInstance().getString('bbb.desktopPublish.fullscreen.label')}" visible="true" click="shareScreen(true)" />
					<mx:Spacer width="100%"/>
					<mx:Button id="btnRegionPublish" toolTip="{ResourceUtil.getInstance().getString('bbb.desktopPublish.region.tooltip')}" label="{ResourceUtil.getInstance().getString('bbb.desktopPublish.region.label')}" visible="true" click="shareScreen(false)" />		
				</mx:ControlBar>
            </mx:AddChild>
        </mx:State>		
        <mx:State name="dispQuitControlBar">   
            <mx:AddChild>
				<mx:ControlBar id="quitBottomBar">
					<mx:Spacer width="50%"/>
					<mx:Button id="btnClosePublish" toolTip="{ResourceUtil.getInstance().getString('bbb.desktopPublish.stop.tooltip')}" label="{ResourceUtil.getInstance().getString('bbb.desktopPublish.stop.label')}" visible="true" enabled="false" click="stopSharing()" />
					<mx:Spacer width="50%"/>
				</mx:ControlBar>
            </mx:AddChild>
        </mx:State>	
	</dspub:states>
				
	<mx:Image id="cursorImg" visible="false" source="@Embed('../../assets/images/cursor4.png')"/>
	

</dspub:MDIWindow>
